<head>
<script src="processing.min.js"></script>
</head>
<body>
<script data-processing-target="mycanvas" type="text/processing">
  PImage b;
  ArrayList<Point> myArr;
  int starNum = 800;
  int starLimit = 500;
  int textSize=30;
  PFont font;
  String textArray[] = {"밤 하늘에 쓰여진 별들\nStars written in the night sky"};
  int idx = 0;

  Star myStar[] = new Star[starNum];


  void setup()
  {
    size(800,500);
    b = loadImage("bg.png");
    myArr = new ArrayList();
    font = loadFont("휴먼가는샘체-70.vlw");
    imageMode(CENTER);
    b.resize(width,height);
    for(int i=0;i<starNum;i++)
      myStar[i] = new Star();

    textSize = width/15;
    setText(textArray[idx]);
  }
  void draw()
  {
   image(b,width/2,height/2);
   for(int i=0;i<starLimit;i++)
    {
      if(i%200==0)
      {
       myStar[i].myStar();
       myStar[i].myDisplay();

      }
     else if(i%150==0)
     {
       myStar[i].drop();
       myStar[i].dropDisplay();

     }
     else
     {
       myStar[i].shine();
       myStar[i].display();
     }
   }

   for(int i=0;i<myArr.size();i++){
     myArr.get(i).update();
     myArr.get(i).display();
   }

   if(mousePressed)
   {
     Point temp = new Point(mouseX,mouseY);
     myArr.add(temp);
   }
   //auto();
   description();

  }

  void description()
  {
   textSize = width/20;
   fill(200+random(-30,30),180);
   text("Click to Screen",width-width/5,height-height/20);
   textSize = width/15;
  }

  void auto()
  {
    for(int i=0;i<myArr.size();i++)
    {
      if(!myArr.get(i).isEnd())
        return;
    }
    textChange();
  }


  void textChange()
  {
    myArr.clear();
    idx++;

    if(idx==textArray.length)
      idx = 0;

    setText(textArray[idx]);
  }

  void mouseClicked()
  {
    if(mouseButton == RIGHT)
      textChange();
  }


  void setText(String txt)
  {
  PGraphics pg = createGraphics(width, height);
  pg.beginDraw();
  pg.fill(0);
  pg.textAlign(CENTER);
  pg.textFont(font,textSize);
  pg.text(txt,width/2,height/2);
  pg.endDraw();
  pg.loadPixels();

    int val = 4;

    if(width>1100)
      val = 4;
    else if(width>500)
      val =3;
    else
      val =2;

  for(int h=0;h<height;h+=val)
    for(int w=0;w<width;w+=val)
    {
       if(pg.pixels[width*h+w]!=0){
         Point temp = new Point(w,h);
         myArr.add(temp);
       }
    }
  }

    // 포인트 이벤트
  class Point
  {
    PVector pos;
    PVector des;
    PVector vel;
    PVector acc;

    float limit = 1;
    int speed = (int)random(5,12);
    float size = random(1,6);

    Point(float x, float y)
    {
      pos = new PVector(random(0,width),random(0,height));
      des = new PVector(x,y);
      vel = new PVector(0,0);
      acc = new PVector(0,0);
    }

    void display()
    {
       fill(255);
       ellipse(pos.x,pos.y,size,size);
    }

    void update()
    {
      float distance = dist(pos.x,pos.y,des.x,des.y);
      float proximityMult = 1.0;

      if(distance<50)
      {
          proximityMult = distance/50;
      }

      //벡터의 방향과 크기를 정해준다.
        PVector toward = new PVector(this.des.x,this.des.y);
        toward.sub(pos);
        toward.normalize();
        toward.mult(proximityMult*speed);


        PVector steer = new PVector(toward.x,toward.y);
        steer.sub(vel);


        acc.add(steer);
        vel.add(acc);
        pos.add(vel);

        acc.mult(0);
    }

    boolean isEnd()
    {
      float distance = dist(pos.x,pos.y,des.x,des.y);
      if(distance <5)
        return true;
      else
        return false;
    }
  }

  //별 효과 모음
  class Star
  {
    float xpos, ypos;
    PVector location;
    float brightness;
    float starSize;
    float light;
    float dropSpeed = random(3,7);

    PVector myStarLoc[] = new PVector[4];


    Star()
    {
      noStroke();
      location = new PVector(random(width),random(height));
      starSize = random(3,7);
      light = random(1,7);
      brightness =30;
      for(int i =0;i<4;i++)
      {
        myStarLoc[i] = new PVector(random(location.x-50,location.x+50),
        random(location.y-50,location.y+50));
      }
    }

    void display()
    {
      for(int i=0;i<starSize;i++)
      {
        fill(255,brightness-i*5);
        ellipse(location.x,location.y,i,i);
      }
    }

    //빛나는 효과
    void shine()
    {
      if(brightness>255){
        brightness =255;
        light = random(-1,-7);

      }
      if(brightness<0)
      {
       location = new PVector(random(width),random(height));
        light = random(1,7);
        starSize =  random(3,6);
        brightness =0 ;
      }
       brightness +=light;

    }

    void drop()
    {
      if(location.x >width || location.y>height)
      {
        location.x = random(width);
        location.y = 0;
        dropSpeed = random(2,6);
      }
      location.x+=dropSpeed;
      location.y+=dropSpeed;

    }

    void dropDisplay()
    {
      fill(255);
      for(float i=5;i>0;i-=1){
        ellipse(location.x-i,location.y-i,starSize/i,starSize/i);
      }
      ellipse(location.x,location.y,starSize,starSize);
    }

      //별자리
  void myDisplay()
  {
    fill(255,brightness);
    pushMatrix();
    stroke(255,brightness);
    ellipse(location.x,location.y,starSize,starSize);
    line(location.x,location.y,myStarLoc[0].x,myStarLoc[0].y);
    ellipse(myStarLoc[0].x,myStarLoc[0].y,starSize,starSize);
    line(location.x,location.y,myStarLoc[1].x,myStarLoc[1].y);
    ellipse(myStarLoc[1].x,myStarLoc[1].y,starSize,starSize);
    line(myStarLoc[1].x,myStarLoc[1].y,myStarLoc[2].x,myStarLoc[2].y);
    ellipse(myStarLoc[2].x,myStarLoc[2].y,starSize,starSize);
    line(myStarLoc[2].x,myStarLoc[2].y,myStarLoc[3].x,myStarLoc[3].y);
    ellipse(myStarLoc[3].x,myStarLoc[3].y,starSize,starSize);
    popMatrix();
    noStroke();
  }

  void myStar()
  {
  if(brightness>255){
      brightness =255;
      light = random(-1,-7);
    }
    if(brightness<0)
    {
     location = new PVector(random(width),random(height));
     for(int i =0;i<4;i++)
      {
        myStarLoc[i] = new PVector(random(location.x-50,location.x+50),
        random(location.y-50,location.y+50));
      }
      light = random(1,7);
      starSize =  random(3,7);
      brightness =0 ;
    }
     brightness +=light;
     for(int i =0;i<4;i++)
      {
        myStarLoc[i].x += noise(1)*random(-2,2);
        myStarLoc[i].y += noise(1)*random(-2,2);
      }
  }


  }

</script>
<p align=CENTER>
<canvas id="mycanvas"></canvas>
</p>
</body>
</html>
